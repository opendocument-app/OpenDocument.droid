apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 31
    ndkVersion "23.1.7779620"

    defaultConfig {
        applicationId "at.tomtasche.reader"
        minSdkVersion 19
        targetSdkVersion 31

        testApplicationId "at.tomtasche.reader.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        vectorDrawables.useSupportLibrary true

        externalNativeBuild.cmake.targets "odr-core"
        externalNativeBuild.cmake.arguments "-DANDROID_STL=c++_shared"
    }

    dexOptions.javaMaxHeapSize "4g"

    flavorDimensions "default"

    signingConfigs {
        releasePro {
            storeFile file("../../google_play.keystore")
            storePassword "releaseme"
            keyAlias "reader-pro"
            keyPassword "releaseme"
        }

        releaseLite {
            storeFile file("../../google_play.keystore")
            storePassword "releaseme"
            keyAlias "reader"
            keyPassword "releaseme"
        }
    }

    productFlavors {
        lite {
            signingConfig signingConfigs.releaseLite

            resValue("bool", "DISABLE_TRACKING", "false")
        }

        pro {
            applicationIdSuffix ".pro"
            versionNameSuffix "-pro"

            signingConfig signingConfigs.releasePro

            resValue("bool", "DISABLE_TRACKING", "true")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'

            firebaseCrashlytics.nativeSymbolUploadEnabled true
            ndk.debugSymbolLevel = "full"
        }
    }

    android.bundle.language.enableSplit false


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            version "3.18.0+"
            path file('../CMakeLists.txt')
        }
    }
    lint {
        abortOnError false
    }
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:29.0.4')
    implementation 'com.google.firebase:firebase-storage'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-perf'
    implementation 'com.google.firebase:firebase-crashlytics-ndk'
    implementation 'com.google.firebase:firebase-inappmessaging-display'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-config'

    implementation 'com.google.android.gms:play-services-ads:20.5.0'

    implementation 'com.android.billingclient:billing:4.0.0'

    implementation 'com.google.android.gms:play-services-ads:20.5.0'
    implementation 'com.google.android.play:core:1.10.3'

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core:1.7.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.webkit:webkit:1.4.0'

    implementation 'com.github.AppIntro:AppIntro:6.2.0'

    implementation('com.nononsenseapps:filepicker:4.2.1', {
        exclude group: 'com.android.support'
    })

    implementation 'com.viliussutkus89:pdf2htmlex-android:0.18.16'
    implementation 'com.viliussutkus89:wvware-android:1.2.4'
    implementation 'com.github.huzongyao:AndroidMagic:v1.1.2'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    // espresso-idling-resource is used in main sourceSet as well. cannot be just androidTestImplementation
    implementation 'androidx.test.espresso:espresso-idling-resource:3.4.0'
    implementation 'androidx.annotation:annotation:1.3.0'
}

apply plugin: 'com.google.gms.google-services'
