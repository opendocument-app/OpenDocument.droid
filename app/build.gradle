import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.android.application'
    id 'app.opendocument.conanandroidgradleplugin'
}

android {
    ndkVersion "28.1.13356709"
}

tasks.register('conanProfile', Copy) {
    from "conanprofile.txt"
    into project.layout.buildDirectory
    filter(ReplaceTokens, tokens: ["NDK_PATH": android.ndkDirectory.toString()])
}

["armv8", "armv7", "x86", "x86_64"].each { architecture ->
    tasks.named("conanInstall-" + architecture) {
        profile.set('build/conanprofile.txt')
        deployer.set('conandeployer.py')
        deployerFolder.set(outputDirectory.get().asFile.toString() + "/assets/core")
        dependsOn(tasks.named('conanProfile'))
        conanExecutable.set('conan')
    }
}

android {
    defaultConfig {
        applicationId "at.tomtasche.reader"
        minSdkVersion 23
        compileSdkVersion 35
        targetSdkVersion 35

        testApplicationId "at.tomtasche.reader.test"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        vectorDrawables.useSupportLibrary true

        externalNativeBuild {
            cmake {
                targets "odr-core"
                arguments(
                    "-DANDROID_STL=c++_shared",
                    "-DCMAKE_TOOLCHAIN_FILE=build/conan/android_toolchain.cmake",
                    "-DCMAKE_BUILD_TYPE=RelWithDebInfo",
                )
            }
        }
    }

    flavorDimensions "default"

    signingConfigs {
        releasePro {
            storeFile file("../../google_play.keystore")
            storePassword "releaseme"
            keyAlias "reader-pro"
            keyPassword "releaseme"
        }

        releaseLite {
            storeFile file("../../google_play.keystore")
            storePassword "releaseme"
            keyAlias "reader"
            keyPassword "releaseme"
        }
    }

    productFlavors {
        lite {
            signingConfig signingConfigs.releaseLite

            resValue("bool", "DISABLE_TRACKING", "false")
        }

        pro {
            applicationIdSuffix ".pro"
            versionNameSuffix "-pro"

            signingConfig signingConfigs.releasePro

            resValue("bool", "DISABLE_TRACKING", "true")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
            ndk.debugSymbolLevel = "full"
        }
    }

    android.bundle.language.enableSplit false

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            version "3.22.0+"
            path "CMakeLists.txt"
        }
    }
    lint {
        abortOnError false
    }
    buildFeatures {
        buildConfig = true
    }
    packagingOptions {
        jniLibs {
            // No need to pickFirst libc++_shared.so if all files are identical.
            // They will be identical if NDK major version matches.
            // NDK runtime problems may occur if NDK version mismatches,
            // so comment this out, to get a compile error instead
            // pickFirsts += ['**/libc++_shared.so']
        }
    }
    namespace 'at.tomtasche.reader'
    compileSdk 35

    // TODO can and should this be architecture dependent?
    sourceSets.main.assets.srcDirs += "build/conan/armv8/assets"
}

dependencies {
    implementation 'com.google.android.gms:play-services-ads:24.3.0'
    implementation 'com.google.android.play:review:2.0.2'
    implementation 'com.google.android.ump:user-messaging-platform:3.1.0'

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core:1.15.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.webkit:webkit:1.12.1'

    implementation 'com.viliussutkus89:assetextractor-android:1.3.3'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.6.1'
    // espresso-idling-resource is used in main sourceSet as well. cannot be just androidTestImplementation
    implementation 'androidx.test.espresso:espresso-idling-resource:3.6.1'
    implementation 'androidx.annotation:annotation:1.9.1'
}

// Without removing .cxx dir on cleanup, double gradle clean is erroring out.
// Before removing this workaround, check if "./gradlew assembleDebug; ./gradlew clean; ./gradlew clean" works
tasks.named("clean") {
    def dotCxxDir = layout.projectDirectory.dir(".cxx")
    doFirst {
        delete dotCxxDir
    }
}
